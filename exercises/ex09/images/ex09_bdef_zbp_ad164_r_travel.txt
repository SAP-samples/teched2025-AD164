
managed implementation in class zbp_ad164_r_travel_### unique;
strict ( 2 );
with draft;

with privileged mode disabling NoCheckWhenPrivileged;
define authorization context NoCheckWhenPrivileged { '/DMO/TRVL'; }
define own authorization context by privileged mode;

define behavior for ZAD164_R_Travel_### alias Travel
persistent table zad164_trvl_###
draft table zad164_trvld_###
lock master total etag LastChangedAt
authorization master ( instance )
etag master LocalLastChangedAt
with additional save with full data
{
field ( numbering : managed, readonly ) TravelUUID;
association _Booking { create; with draft; }

create;
update;
delete;

event statusUpdated parameter ZAD164_A_StatusUpdated_###;

action ( features : instance, authorization : update ) acceptTravel result [1] $self;
action ( features : instance, authorization : update ) rejectTravel result [1] $self;

draft action Resume;
draft action Edit;
draft action Activate optimized;
draft action Discard;
draft determine action Prepare;

mapping for zad164_trvl_###
 {
   TravelUUID         = travel_uuid;
   TravelID           = travel_id;
   AgencyID           = agency_id;
   CustomerID         = customer_id;
   BeginDate          = begin_date;
   EndDate            = end_date;
   BookingFee         = booking_fee;
   TotalPrice         = total_price;
   CurrencyCode       = currency_code;
   Description        = description;
   OverallStatus      = overall_status;
   LocalCreatedBy     = local_created_by;
   LocalCreatedAt     = local_created_at;
   LocalLastChangedAt = local_last_changed_at;
   LocalLastChangedBy = local_last_changed_by;
   LastChangedAt      = last_changed_at;
 }
}

define behavior for ZAD164_R_Booking_### alias Booking
persistent table zad164_book_###
draft table zad164_bookd_###
lock dependent by _Travel
authorization dependent by _Travel
etag master LocalLastChangedAt
{

field ( numbering : managed, readonly ) BookingUUID;
field ( readonly ) TravelUUID;

update;
delete;
association _Travel { with draft; }

mapping for zad164_book_###
 {
   BookingUUID        = booking_uuid;
   TravelUUID         = parent_uuid;
   BookingID          = booking_id;
   BookingDate        = booking_date;
   CustomerID         = customer_id;
   AirlineID          = carrier_id;
   ConnectionID       = connection_id;
   FlightDate         = flight_date;
   FlightPrice        = flight_price;
   CurrencyCode       = currency_code;
   BookingStatus      = booking_status;
   LocalLastChangedAt = local_last_changed_at;
 }
}
